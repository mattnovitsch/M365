let LastLogins=DeviceLogonEvents
    | where LogonType == "Interactive"
    | where InitiatingProcessParentFileName == "wininit.exe"
    | summarize LastLogon=arg_max(Timestamp, *) by AccountName, DeviceName
    | project AccountName, DeviceName, LastLogon;
let Logins=DeviceLogonEvents
    | where LogonType == "Interactive"
    | where InitiatingProcessParentFileName == "wininit.exe"        
    | summarize Logins=count() by AccountName, DeviceName
    | project AccountName, Logins, DeviceName;
let NetworkInfo=DeviceNetworkInfo
    | where IPv4Dhcp <> ""
    | mvexpand parse_json(IPAddresses)
    | where IPAddresses.IPAddress !contains ":"
    | summarize arg_max(Timestamp, *) by DeviceName
    | project DeviceName, IPAddress=IPAddresses.IPAddress, Timestamp;
Logins
| join kind=inner ( LastLogins
    | project AccountName, DeviceName, LastLogon
) on DeviceName, AccountName
| join kind=leftouter  ( NetworkInfo
    | project DeviceName, IPAddress, Timestamp
) on DeviceName
| project AccountName, DeviceName, LastLogon, Logins, IPAddress
| sort by DeviceName